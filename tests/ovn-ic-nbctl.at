AT_BANNER([ovn-ic-nbctl])

# OVN_IC_NBCTL_TEST_START
m4_define([OVN_IC_NBCTL_TEST_START],
  [dnl Create database (ovn-ic-nb).
   AT_KEYWORDS([ic_nbctl])
   AT_CHECK([ovsdb-tool create ovn-ic-nb.db $abs_top_srcdir/ovn-ic-nb.ovsschema])

   dnl Start ovsdb-servers.
   AT_CHECK([ovsdb-server --detach --no-chdir --pidfile=ovn_ic_nb_db.pid --unixctl=$OVS_RUNDIR/ovn_ic_nb_db.ctl --log-file=ovsdb_ic_nb.log --remote=punix:$OVS_RUNDIR/ovn_ic_nb_db.sock ovn-ic-nb.db ], [0], [], [stderr])
   on_exit "kill `cat ovn_ic_nb_db.pid`"
   AT_CHECK([[sed < stderr '
/vlog|INFO|opened log file/d
/ovsdb_server|INFO|ovsdb-server (Open vSwitch)/d']])
   AT_CAPTURE_FILE([ovsdb-server.log])
])

# OVN_IC_NBCTL_TEST_STOP
m4_define([OVN_IC_NBCTL_TEST_STOP],
  [AT_CHECK([check_logs "$1"])
   OVS_APP_EXIT_AND_WAIT_BY_TARGET([$OVS_RUNDIR/ovn_ic_nb_db.ctl], [$OVS_RUNDIR/ovn_ic_nb_db.pid])])

dnl ---------------------------------------------------------------------

AT_SETUP([ovn-ic-nbctl])
OVN_IC_NBCTL_TEST_START

AT_CHECK([ovn-ic-nbctl ts-add ts0])
AT_CHECK([ovn-ic-nbctl ts-list | uuidfilt], [0], [dnl
<0> (ts0)
])

AT_CHECK([ovn-ic-nbctl ts-add ts1])
AT_CHECK([ovn-ic-nbctl ts-list | uuidfilt], [0], [dnl
<0> (ts0)
<1> (ts1)
])

AT_CHECK([ovn-ic-nbctl show | sort], [0], [dnl
Transit_Switch ts0
Transit_Switch ts1
])

AT_CHECK([ovn-ic-nbctl ts-del ts1])
AT_CHECK([ovn-ic-nbctl ts-list | uuidfilt], [0], [dnl
<0> (ts0)
])

AT_CHECK([ovn-ic-nbctl ts-add ts0], [1], [],
  [ovn-ic-nbctl: ts0: a transit switch with this name already exists
])

AT_CHECK([ovn-ic-nbctl --may-exist ts-add ts0])
AT_CHECK([ovn-ic-nbctl ts-list | uuidfilt], [0], [dnl
<0> (ts0)
])

AT_CHECK([ovn-ic-nbctl ts-del ts2], [1], [],
  [ovn-ic-nbctl: ts2: switch name not found
])

AT_CHECK([ovn-ic-nbctl --if-exists ts-del ts2])

OVN_IC_NBCTL_TEST_STOP
AT_CLEANUP
